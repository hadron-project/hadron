// Parser definition for namespaced permission grants.
//
// Examples:
// ns-abc all;
// ns_xyz schema;
// ns0001 stream/billing=all; endpoint/endpoints.>=pub; exchange/team1.messages=sub;

/////////////
// Imports //
// NOTE: copy/paste of common.pest until imports land.

// Characters allowed in a basic object name.
basic_name_chars = _{ ASCII_ALPHANUMERIC | "-" | "_" }
// Characters allowed in a hierarchical object name, such as streams, endpoints, pipelines &c.
hierarchical_name_chars = _{ basic_name_chars | "." }

// Parser for namespace names.
namespace_name = { basic_name_chars{1,100} }
// Parser for object names which can form hierarchies.
object_name = { hierarchical_name_chars{1,100} }

// Wildcards used to match hierarchies.
wildcard = _{ "*" | ">" }

// Imports //
/////////////

// Assign some pub/sub access level.
assign = _{ "=" }
// Pub/sub access levels which may be granted on objects.
pub_sub_access = { "pub" | "sub" | "all" }
// A name segment composed of basic name chars or a wildcard.
name_or_wildcard = _{ (!wildcard ~ basic_name_chars+) | (!basic_name_chars ~ wildcard{1}) }
// A parser for matching a hierarchical name which may have wildcards.
hierarchical_name_matcher = { (name_or_wildcard ~ ("." ~ name_or_wildcard)*){1,100} }

ns_grant_all = { "all;" }
ns_grant_schema = { "schema;" }
ns_grant_exchange = {
    "exchange/" ~ hierarchical_name_matcher ~ assign ~ pub_sub_access ~ ";"
}
ns_grant_endpoint = {
    "endpoint/" ~ hierarchical_name_matcher ~ assign ~ pub_sub_access ~ ";"
}
ns_grant_stream = {
    "stream/" ~ hierarchical_name_matcher ~ assign ~ pub_sub_access ~ ";"
}
ns_grant = {
    ns_grant_all | ns_grant_schema
    | ((ns_grant_exchange | ns_grant_endpoint | ns_grant_stream) ~ (" ")?)+
}

// A namespaced permissions grant.
grant = { SOI ~ namespace_name ~ "=" ~ ns_grant ~ EOI }
